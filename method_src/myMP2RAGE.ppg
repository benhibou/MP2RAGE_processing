;****************************************************************
;
; Copyright (c) 2001 - 2011
; Bruker BioSpin MRI GmbH
; D-76275 Ettlingen, Germany
;
; All Rights Reserved
; 
; MDEFT - a gradient echo imaging method
;
;****************************************************************
;
; d1 - TR padding
; d2 - T2 padding
; d3 = PVM_RiseTime
; d4 = PVM_RampTime
; d8 = SCON / BLKTR_MAN (Amplifier preblanking)

#include <MRI.include>
preset off

define delay denab

define list<phase> phaselist = {$RFPhaseList}

"denab =  d3 - de"
"l6 = l4 + l13"

#include <PrepModulesHead.mod> 


INIT_DEVICES

define list<frequency> mdeftlist = {$Mdeft_PrepFL} 

#define r2d ACQ_spatial_phase_1
#define r3d ACQ_spatial_phase_2

grad_zero_for_gs <r2d, r3d>

decr0,  r2d.dec
        lo to decr0 times l14

        10u recph ph1
start,  10u

;----------------------------------start of the main loop ----------
slice, 	10u

subr TriggerPhase()
#include <MdeftPrep.mod>
;----------------------------------preparation modules -------------
subr FovSat()
subr FatSup()

if( l12 >=1 )                      ;dummy echoes
{
ddslice2,  10u fq8(receive):f1

        d6	grad_ramp{0, 0, g9}
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} fq1:f1
        d8	gatepulse 1
	(p0:sp0 phaselist):f1
	d4 	grad_off
;----------------------slice rephase, read dephase, phase encoding----
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
;----------------------------------frequency encoding---------------
        denab   grad_ramp{g5, 0, 0} 
	de
        aqq		
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d13     grad_ramp{g6, 0, 0}
	d3	grad_off
        d1	
;----------------------------------slice and movie loop-------------
        d20
;----------------------------------2d loop--------------------------
        1u  phaselist.inc    ;phase list for RF spoiling
	lo to ddslice2 times l12
}

slice2,  10u fq8(receive):f1

        d6	grad_ramp{0, 0, g9}
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} fq1:f1
        d8	gatepulse 1
	(p0:sp10 phaselist):f1
	d4 	grad_off
;----------------------slice rephase, read dephase, phase encoding----
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
;----------------------------------frequency encoding---------------
        denab   grad_ramp{g5, 0, 0} 
	ADC_INIT_B(NOPH, phaselist)  ;ph1 is used (see recph)
        aqq	ADC_START	
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d13     grad_ramp{g6, 0, 0}
	d3	grad_off
        d1	ADC_END
;----------------------------------slice and movie loop-------------
        d20
;----------------------------------2d loop--------------------------
        1u  phaselist.inc    ;phase list for RF spoiling
	r2d.inc
	lo to slice2 times l5
        d14 r2d.res
slice3,  10u fq8(receive):f1

        d6	grad_ramp{0, 0, g9}
;----------------------------------slice selection------------------	
	d3 	grad_ramp{0, 0, g0} fq1:f1
        d8	gatepulse 1
	(p10:sp0 phaselist):f1
	d4 	grad_off
;----------------------slice rephase, read dephase, phase encoding----
	d10 	grad_ramp{g2, r2d*g3, g1+r3d*g4}
	d2 	grad_off
;----------------------------------frequency encoding---------------
        denab   grad_ramp{g5, 0, 0} 
	ADC_INIT_B(NOPH, phaselist)  ;ph1 is used (see recph)
        aqq	ADC_START	
;----------------------------------read spoiler + phase encoding----
        d11     grad_ramp{g6, r2d*g7, r3d*g8} ; 2nd and 3rd dims
	d13     grad_ramp{g6, 0, 0}
	d3	grad_off
        d1	ADC_END
;----------------------------------slice and movie loop-------------
        d20
;----------------------------------2d loop--------------------------
        1u  phaselist.inc    ;phase list for RF spoiling
	r2d.inc
	lo to slice3 times l5
	lo to slice times l6
        "l6 = l4"
;----------------------------------3d loop--------------------------
	r3d.inc
	lo to start times l2
        2.5u
        lo to start times NAE
        2.5u
        lo to start times NR
SETUP_GOTO(start)

exit

ph0 = 0
ph1 = 0 




	





